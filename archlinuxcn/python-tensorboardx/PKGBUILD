# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=tensorboardX
pkgname=python-tensorboardx
pkgver=1.6
pkgrel=1
pkgdesc='tensorboard for pytorch (and chainer, mxnet, numpy, ...)'
arch=(any)
url=https://github.com/lanpa/tensorboardX
license=(MIT)
depends=(python-numpy python-protobuf python-six)
makedepends=(python-setuptools)
checkdepends=(flake8 python-future python-matplotlib python-pytest python-pytorch python-torchvision)
source=("${_pkgname}-${pkgver}.tar.gz"::"https://github.com/lanpa/tensorboardX/archive/v${pkgver}.tar.gz")
sha512sums=('beaf7e28458e24fdc3dbff55057234d3776fea7b039facc9456f45b9b8dc51b336f1c041e8a4968b06f19cd65c6339e7eba9a03fb95364dc3a4ca25384ccf4bd')

prepare() {
  cd "${_pkgname}-${pkgver}"
  _ver=$(protoc --version|cut -d ' ' -f 2)
  # using protobuf from official repo
  sed -i "s/3.6.1/${_ver}/" compile.sh
}


build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_pkgname}-${pkgver}"
  pytest -v
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
